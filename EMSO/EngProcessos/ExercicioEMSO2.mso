# Especie de importacao de bibliotecas padroes do EMSO
# Para mais informacoes eml --> types.mso
using "types";

Model corrente
	PARAMETERS
	tamanho as Integer;
	SET 
	tamanho = 3;
	
	# Parametros sao valores fixos. Que nao muda
	# Aqui, ha somente a declaracao dos mesmos
	PARAMETERS
	A as Integer;
	B as Integer;
	C as Integer;
	Cp(tamanho) as positive (Brief="Capacidades calorificas", Unit='kcal/(kg*K)'); 
	lambda(tamanho) as positive (Brief="Calores latente", Unit='kcal/kg');
	
	SET
	A = 1;
	B = 2;
	C = 3;
	Cp = [0.6, 0.8, 1]*'kcal/(kg*K)';
	lambda = [0, 0, 500]*'kcal/kg';
	
	# Variaveis que podem mudar durante a analise
	VARIABLES
	W as positive (Brief="Vazao massica total da corrente", Unit='kg/h');
	x(tamanho) as fraction (Brief="Vetor de fracao massica de cada componente");
	T as positive (Brief="Temperatura da corrente", Unit='K');
	f(tamanho) as positive (Brief="Vetor de vazao massica de cada componente", Unit='kg/h');
	som_comp as positive (Brief="Soma das composicoes");
	
	EQUATIONS
	# Por mais que tenha sido declarado uma variavel, os vetores x e f possuem 3 dimensoes
	# Assim sendo, aqui, ha 3 equacoes
	"Vazao massica de cada componente"
	f = x*W ;
	
	# Inplica que preciso dar todos os x's
	"Soma das composicoes"
	som_comp = sum(x);
	
end

Model fonte
	
	ATTRIBUTES
	Pallete = true;
	Icon = "Icons/fonte";
		
	VARIABLES
	out outlet as corrente (Brief="Corrente de saida");
	
end

FlowSheet teste_fonte
	# É uma boa prática testar o modelo
	# As vezes, se eu especifico de mais, posso ter
	# problema (mesmo com G = 0) --> Eq. certas
	DEVICES
	alimentacao as fonte;
	
	SPECIFY
	alimentacao.outlet.W = 5000*'kg/h';
	alimentacao.outlet.T = 30*'K';
	alimentacao.outlet.x = [0.2, 0.8];
	
	
end

Model trocador_calor
	
	ATTRIBUTES
	Pallete = true;
	Icon = "Icons/extrator";
	
	PARAMETERS
	Ut as positive (Brief="Coef global de troca termica no trocador de calor", Unit='kcal/(h*m^2*K)'); 
	
	SET
	Ut = 100*'kcal/(h*m^2*K)';	
	
	# in --> Corrente de entrada
	# out --> Corrente de saida
	VARIABLES
	in fria_in as corrente (Brief="Corente fria de entrada");
	out fria_out as corrente (Brief="Corente de fria de saida (esquentada)");
	in quente_in as corrente (Brief="Corente quente de entrada");
	out quente_out as corrente (Brief="Corente quente de saida (esfriada)");
	Q as positive (Brief="Calor trocado entre as correntes", Unit='kcal/h');	
	At as positive (Brief="Area util de troca terminca", Unit='m^2');
	DT1 as positive (Brief="Dif de temperaturas de aproach 1", Unit='K');
	DT2 as positive (Brief="Dif de temperaturas de aproach 2", Unit='K');
	DTML as positive (Brief="Dif media logaritmica de temperaturas", Unit='K');
	
	EQUATIONS
	
	"Balance de massa por componente corrente fria"
	fria_in.f = fria_out.f;

	"Balance de massa por componente corrente quente"
	quente_in.f = quente_out.f;
	
	"Composicao aquecida saida"
	sum(fria_out.x) = 1;
	
	"Composicao resfriada saida"
	sum(quente_out.x) = 1;
	
	"Delta T de approach 1"
	DT1 = quente_in.T - fria_out.T;
	
	"Delta T de approach 1"
	DT2 = quente_out.T - fria_in.T;

	"Dif media logaritmica de temperaturas"
	DTML*(ln(DT1/DT2)) = (DT1-DT2);

	"Dimensionamento"
	Q = Ut*At*DTML;

	"Balanco de Energia corrente fria"
	Q = (sum(fria_in.Cp*fria_in.f))*(fria_out.T-fria_in.T);
	
	"Balanco de Energia corrente quente"
	Q = (sum(quente_in.Cp*quente_in.f))*(quente_in.T-quente_out.T);

end

Model aquecedorComVapor
	
	ATTRIBUTES
	Pallete = true;
	Icon = "Icons/extrator";
	
	PARAMETERS
	Ua as positive (Brief="Coef global de troca termica no aquecedor", Unit='kcal/(h*m^2*K)'); 
	
	SET
	Ua = 500*'kcal/(h*m^2*K)';	
	
	# in --> Corrente de entrada
	# out --> Corrente de saida
	VARIABLES
	in fria_in as corrente (Brief="Corente fria de entrada");
	out fria_out as corrente (Brief="Corente de fria de saida (esquentada)");
	in quente_in as corrente (Brief="Corente quente de entrada");
	out quente_out as corrente (Brief="Corente quente de saida (esfriada)");
	Q as positive (Brief="Calor trocado entre as correntes", Unit='kcal/h');	
	Aa as positive (Brief="Area util de troca terminca", Unit='m^2');
	DT1 as positive (Brief="Dif de temperaturas de aproach 1", Unit='K');
	DT2 as positive (Brief="Dif de temperaturas de aproach 2", Unit='K');
	DTML as positive (Brief="Dif media logaritmica de temperaturas", Unit='K');
	
	EQUATIONS
	
	"Balance de massa por componente corrente fria"
	fria_in.f = fria_out.f;

	"Balance de massa por componente corrente quente"
	quente_in.f = quente_out.f;
	
		
	"Composicao resfriada saida"
	sum(fria_out.x) = 1;
	
	
	"Soma composicao vapor"
	sum(quente_out.x) = 1;
	#sum(quente_in.x) = 1;	
		

	"Balanco de Energia corrente fria"
	Q = (sum(fria_in.Cp*fria_in.f))*(fria_out.T-fria_in.T);
	
	"Balanco de energia do vapor"
	Q = quente_in.W*quente_in.lambda(quente_in.C);
	
	"Delta T de approach 1"
	DT1 = quente_in.T - fria_out.T;
	
	"Delta T de approach 1"
	DT2 = quente_out.T - fria_in.T;

	"Dif media logaritmica de temperaturas"
	DTML*(ln(DT1/DT2)) = (DT1-DT2);
	
	"Dimensionamento"
	Q = Ua*Aa*DTML;	
	
	"Vapor so muda de fase"
	quente_in.T = quente_out.T;

end

FlowSheet Dimensionamento

	DEVICES
	AlimentacaoQuente as fonte;
	AlimentacaoFria as fonte;
	Vapor as fonte;
	Agua as fonte;
	
	Trocador1 as trocador_calor;
	Trocador2 as trocador_calor;
	AquecedorComVapor as aquecedorComVapor;
	
	CONNECTIONS
	AlimentacaoQuente.outlet to Trocador1.quente_in;
	AlimentacaoFria.outlet to Trocador1.fria_in;
	Trocador1.quente_out to Trocador2.quente_in;
	Agua.outlet to Trocador2.fria_in;	
	Trocador1.fria_out to AquecedorComVapor.fria_in;
	Vapor.outlet to AquecedorComVapor.quente_in;
	
	
	SPECIFY
	
	AlimentacaoQuente.outlet.T = (500)*'K';
	AlimentacaoQuente.outlet.W = 3000*'kg/h';
	AlimentacaoQuente.outlet.x = [1, 0, 0];
	
	AlimentacaoFria.outlet.T = (350)*'K';
	AlimentacaoFria.outlet.W = 5000*'kg/h';
	AlimentacaoFria.outlet.x = [0, 1, 0];
	
	Vapor.outlet.T = (600)*'K';
	Vapor.outlet.x = [0, 0, 1];
	
	Agua.outlet.T = (300)*'K';
	Agua.outlet.x = [0, 0, 1];
	
	Trocador1.quente_out.T = (450)*'K';

	Trocador2.quente_out.T = (340)*'K';
	AquecedorComVapor.fria_out.T = (560)*'K';
	
	Trocador2.fria_out.T = (Trocador1.quente_out.T*'1/K' - 10)*'K';
	
	
	VARIABLES
	CCt as positive (Brief="Custo de Capital Total de um Trocador", Unit= 'US$/h');
	Cop as positive (Brief="Custo de Operacao", Unit= 'US$/h');
	CT as positive (Brief="Custo Total de Operacao da Unidade", Unit= 'US$/h');
	
	EQUATIONS
	CCt = ((80*(Trocador1.At*'1/m^2')^0.6 + 1500) + (80*(Trocador2.At*'1/m^2')^0.6 + 1500) + (80*(AquecedorComVapor.Aa*'1/m^2')^0.6 + 1500))*'US$/h'; 
	Cop = (0.05*Agua.outlet.W*'h/kg' + 0.15*Vapor.outlet.W*'h/kg')*'US$/h';
	CT = (CCt + Cop);  
	
	OPTIONS
	Dynamic = false;

end

CaseStudy CS as Dimensionamento
	
	VARY
	Trocador1.quente_out.T = [420:0.5:480]*'K';
	
	RESPONSE
	CCt;
	Cop;
	CT;
	
	OPTIONS
	Dynamic = false;

end
