# Especie de importacao de bibliotecas padroes do EMSO
# Para mais informacoes eml --> types.mso
using "types";

Model corrente
	# PARAMETERS
	# tamanho as Integer;
	# SET 
	# tamanho = 2;
	
	# Parametros sao valores fixos. Que nao muda
	# Aqui, ha somente a declaracao dos mesmos
	PARAMETERS
	Soluto as Integer;
	Agua as Integer;
	# Perceber que Cp(3), rho(3), lambda (3)
	# Isso indica que sao vetores de 3 dimensoes
	# n = 3 | dimensao do vetor
	# Unit ==> variavel do parametro
	Cp(2) as positive (Brief="Capacidades calorificas", Unit='kcal/(kg*K)'); 
	#rho(tamanho) as positive (Brief="Densidades", Unit='kg/l');
	lambda(2) as positive (Brief="Calores latente", Unit='kcal/kg');
	
	# SET --> Onde vai colocar o nome numerico do parametro
	# Reparar que 1, 2, 3 sao referentes a ordem nos vetores
	# Exemplo, Cp = 0,44, rho = 1.272 refere-se ao AcBenz = 1
	SET
	Soluto = 1;
	Agua = 2;
	Cp = [0.5, 1]*'kcal/(kg*K)';
	#rho = [1.272, 0.8834, 1]*'kg/l';
	lambda = [94.14, 505]*'kcal/kg';
	
	# Variaveis que podem mudar durante a analise
	VARIABLES
	W as positive (Brief="Vazao massica total da corrente", Unit='kg/h');
	x(2) as fraction (Brief="Vetor de fracao massica de cada componente");
	T as positive (Brief="Temperatura da corrente", Unit='K');
	f(2) as positive (Brief="Vetor de vazao massica de cada componente", Unit='kg/h');
	som_comp as positive (Brief="Soma das composicoes");
	
	EQUATIONS
	# Por mais que tenha sido declarado uma variavel, os vetores x e f possuem 3 dimensoes
	# Assim sendo, aqui, ha 3 equacoes
	"Vazao massica de cada componente"
	f = x*W ;
	
	# Inplica que preciso dar todos os x's
	"Soma das composicoes"
	som_comp = sum(x);
	
	#"Soma das correntes"
	#W = sum(f);
	
end

Model fonte
	
	ATTRIBUTES
	Pallete = true;
	Icon = "Icons/fonte";
		
	VARIABLES
	out outlet as corrente (Brief="Corrente de saida");
	
end

FlowSheet teste_fonte
	# É uma boa prática testar o modelo
	# As vezes, se eu especifico de mais, posso ter
	# problema (mesmo com G = 0) --> Eq. certas
	DEVICES
	alimentacao as fonte;
	
	SPECIFY
	alimentacao.outlet.W = 5000*'kg/h';
	alimentacao.outlet.T = 30*'K';
	alimentacao.outlet.x = [0.2, 0.8];
	
	
end

Model trocador_calor
	
	ATTRIBUTES
	Pallete = true;
	Icon = "Icons/extrator";
	
	PARAMETERS
	Ut as positive (Brief="Coef global de troca termica no trocador de calor", Unit='kcal/(h*m^2*K)'); 
	
	SET
	Ut = 100*'kcal/(h*m^2*K)';	
	
	# in --> Corrente de entrada
	# out --> Corrente de saida
	# Trocador de calor, indentificar: Quente e Fria
	VARIABLES
	in feed as corrente (Brief="Corente de entrada da alimentacao");
	out resfriada as corrente (Brief="Corente de saida resfriada");
	in reciclo as corrente (Brief="Corente de reciclo");
	out aquecida as corrente (Brief="Corente de saida aquecida");
	Q as positive (Brief="Calor trocado entre as correntes", Unit='kcal/h');	
	At as positive (Brief="Area util de troca terminca", Unit='m^2');
	DT1 as positive (Brief="Dif de temperaturas de aproach 1", Unit='K');
	DT2 as positive (Brief="Dif de temperaturas de aproach 2", Unit='K');
	DTML as positive (Brief="Dif media logaritmica de temperaturas", Unit='K');
	
	EQUATIONS
	
	"Balance de massa por componente corrente fria"
	feed.f = aquecida.f;

	"Balance de massa por componente corrente quente"
	reciclo.f = resfriada.f;
	
	"Composicao resfriada saida"
	sum(resfriada.x) = 1;
	
	"Composicao aquecida"
	sum(aquecida.x) = 1;
	
	"Delta T de approach 1"
	DT1 = reciclo.T - aquecida.T;
	
	"Delta T de approach 1"
	DT2 = resfriada.T - feed.T;

	"Dif media logaritmica de temperaturas"
	DTML*(ln(DT1/DT2)) = (DT1-DT2);

	"Dimensionamento"
	Q = Ut*At*DTML;

	# + feed.f(2)*aquecida.Cp(2)
	"Balanco de Energia corrente fria"
	Q = (sum(feed.Cp*feed.f))*(aquecida.T-feed.T);
	
	# + reciclo.f(2)*reciclo.Cp(2)
	"Balanco de Energia corrente quente"
	Q = (sum(reciclo.Cp*reciclo.f))*(reciclo.T-resfriada.T);

	
end

FlowSheet teste_trocador_calor
	
	DEVICES
	Feed as fonte;
	Reciclo as fonte;
	Trocador_calor as trocador_calor;
	
	CONNECTIONS
	# Preciso conectar uma saida com uma entrada
	# Analogia da tomada
	Feed.outlet to Trocador_calor.feed;
	Reciclo.outlet to Trocador_calor.reciclo;
	
	SPECIFY
	# Para trocador de calor: Só preciso definir uma temperatura de saída	
	# Aproveitar o código
	# Mudar o nome de UMA variável, vai aparecer outras linhas dependentes!
	# Maneira de aproveitar o código!
	
	Feed.outlet.W = 5000*'kg/h';
	Feed.outlet.T = 30*'K';
	Feed.outlet.x = [0.2, 0.8];
	
	Reciclo.outlet.W = 1000*'kg/h';
	Reciclo.outlet.T = 80*'K';
	Reciclo.outlet.x = [0.4, 0.6];	
	
	#Reciclo.outlet.T = 100*'K';
	
	Trocador_calor.resfriada.T = 45*'K';
	#Trocador_calor.resfriada.x(1) = 0.4;
	#Trocador_calor.resfriada.x(2) = 0.6;
	
	OPTIONS
	Dynamic = false;

end

Model resfriador
	
	ATTRIBUTES
	Pallete = true;
	Icon = "Icons/trocador";
	
	PARAMETERS
	Ur as positive (Brief="Coef global de troca termica", Unit='kcal/(h*m^2*K)'); 
	
	SET
	Ur = 100*'kcal/(h*m^2*K)';
	
	VARIABLES
	in alimetacao as corrente (Brief="Corrente de entrada do benzeno", PosX=1, PosY=0.5);
	out aquecida as corrente (Brief="Corrente de saida do benzeno", PosX=0, PosY=0.5);
	in reciclo as corrente (Brief="Corente de entrada da agua", PosX=0.72, PosY=1);
	out resfriada as corrente (Brief="Corrente de saida da agua", PosX=0.27, PosY=0);
	Q as positive (Brief="Calor trocado", Unit='kcal/h');
	DT1 as positive (Brief="Dif de temperaturas de aproach 1", Unit='K');
	DT2 as positive (Brief="Dif de temperaturas de aproach 2", Unit='K');
	DTML as positive (Brief="Dif media logaritmica de temperaturas", Unit='K'); 
	Ar as positive (Brief="Area util de troca terminca", Unit='m^2');
	
	# benzeno_in = alimetacao
	# benzeno_out = aquecida
	# agua_in = reciclo
	# agua_out = resfriada
	
	EQUATIONS
	
	"Balanco de massa por componentes corrente benzeno"
	alimetacao.f = aquecida.f;
	
	"Soma das composicoes"
	sum(aquecida.x) = 1;
	
	"Balanco de massa por componentes corrente agua"
	reciclo.f = resfriada.f;
	
	"Soma das composicoes"
	sum(resfriada.x) = 1;
		
	"Balanco de energia da agua"
	Q = (reciclo.f(1)*reciclo.Cp(1)+reciclo.f(2)*reciclo.Cp(2))*(resfriada.T-reciclo.T);
	
	"Balanco de energia do soluto"
	Q = (alimetacao.f(1)*alimetacao.Cp(1) + alimetacao.f(2)*alimetacao.Cp(2))*(alimetacao.T-aquecida.T);
	
	"Dif de temperaturas de aproach 1"
	DT1 = alimetacao.T-resfriada.T;
	
	"Dif de temperaturas de aproach 2"
	DT2 = aquecida.T-reciclo.T;
		
	"Dif media logaritmica de temperaturas"
	DTML*(ln(DT1/DT2)) = (DT1-DT2);
	
	"Area"
	Q = DTML*Ur*Ar;
	
end

FlowSheet teste_resfriador

	# benzeno_in = alimetacao
	# benzeno_out = aquecida
	# agua_in = reciclo
	# agua_out = resfriada

	DEVICES
	Alimentacao as fonte;
	Reciclo as fonte;
	Resfriador as resfriador;
	
	CONNECTIONS
	Alimentacao.outlet to Resfriador.alimetacao;
	Reciclo.outlet to Resfriador.reciclo;
		
	SPECIFY
	
	Alimentacao.outlet.T = 30*'K';
	Alimentacao.outlet.W = 50000*'kg/h';
	Alimentacao.outlet.x(1) = 0.2;
	
	#Agua.outlet.T = 100*'K';
	# Agua.outlet.x = [0, 0, 1];
	
	Resfriador.resfriada.T = 45*'K';
	Resfriador.resfriada.x(1) = 0.4;
		
	OPTIONS
	Dynamic = false;

end
